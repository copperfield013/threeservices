package com.abc.rule.people.ipm

import com.abc.rrc.record.Attribute;
import com.zhsq.biz.constant.CommonAlgorithm;
import com.abc.fuse.improve.attribute.FuseAttribute;
import com.abc.model.enun.ValueType;
import com.zhsq.biz.constant.EnumKeyValue;
import com.abc.fuse.improve.attribute.label.FuseLabelAttribute;
import com.zhsq.biz.people.algorithm.BirthdayIntrospection;
import com.zhsq.biz.common.CommonCalculation;
import java.util.Date;
import com.abc.rrc.factory.DBAttributeFactory;
import com.zhsq.biz.constant.people.PeopleItem;
import com.zhsq.biz.constant.DateUtils;
import com.abc.fuse.improve.attribute.leaf.FuseLeafAttribute;
import com.zhsq.biz.constant.BaseConstant;
import com.abc.ops.builder.RecordRelationOpsBuilder;
import com.zhsq.biz.constant.RelationType;
import com.zhsq.biz.constant.worktask.WorkTaskItem;
import com.abc.transfer.builder.IRootRecordBuilder;
import com.abc.transfer.builder.RootRecordBuilderFactory;
import com.abc.rrc.record.RootRecord;
import com.abc.sysdate.SystemDateTimeManager;
import com.abc.fuse.improve.attribute.FuseLeafAttrFactory;
import com.zhsq.biz.people.algorithm.IDIntrospection;
import com.abc.util.CodeManager;
import com.zhsq.biz.people.algorithm.CommIntrospection;

global java.lang.String hostCode; 
global java.lang.String hostType;

global java.lang.String userCode;
global java.lang.String recordCode;
global java.lang.String recordName;

global java.util.List recordRelationOpsBuilderNew;
global java.util.List rootRecordList;
global java.util.List attributeList;
global java.util.List addedLabelList;
global java.util.List removedLabelList;
global java.util.List addedLabelList;
global java.util.List addedLeafAttrList;
global java.util.Map removedLeafAttrMap;
global com.abc.rrc.record.RootRecord rootRecord;
global  com.abc.ops.builder.RecordRelationOpsBuilder recordRelationOpsBuilder;
global com.abc.complexus.RecordComplexus recordComplexus;

rule "add残障补贴" // 只要是残疾人， 就发送补贴
	salience 20
	when
		not FuseLeafAttribute( name==PeopleItem.已享受的补贴项目_补贴项目, getValue(ValueType.INT) == EnumKeyValue.ENUM_补贴项目_残障补贴) 
		not FuseLeafAttribute( name==PeopleItem.可享受的补贴项目_补贴项目, getValue(ValueType.INT) == EnumKeyValue.ENUM_补贴项目_残障补贴) 
		FuseAttribute( name==PeopleItem.是否残疾, getValue(ValueType.INT)==EnumKeyValue.ENUM_是否_是 )
	then 
		String code = CodeManager.newCode32();
		//补贴项目
		FuseLeafAttribute subPro = FuseLeafAttrFactory.newInstance(recordCode, PeopleItem.可享受的补贴项目,code, PeopleItem.可享受的补贴项目_补贴项目, EnumKeyValue.ENUM_补贴项目_残障补贴);
		addedLeafAttrList.add(subPro);
		
		//预估金额
		FuseLeafAttribute estimateMoney = FuseLeafAttrFactory.newInstance(recordCode, PeopleItem.可享受的补贴项目,code, PeopleItem.可享受的补贴项目_预估补贴金额,  "50元");
		addedLeafAttrList.add(estimateMoney);
		
		//所需材料
		FuseLeafAttribute  material = FuseLeafAttrFactory.newInstance(recordCode, PeopleItem.可享受的补贴项目,code, PeopleItem.可享受的补贴项目_所需材料,  "由社区街道汇总上报名单");
		addedLeafAttrList.add(material);
		
		//享受标准
		FuseLeafAttribute enjoyStandard = FuseLeafAttrFactory.newInstance(recordCode, PeopleItem.可享受的补贴项目,code, PeopleItem.可享受的补贴项目_享受标准,  "每人每月50元");
		addedLeafAttrList.add(enjoyStandard);
end

rule "del残障补贴"
	when
		$fa: FuseLeafAttribute( name==PeopleItem.可享受的补贴项目_补贴项目, getValue(ValueType.INT) == EnumKeyValue.ENUM_补贴项目_残障补贴 )
		(not FuseAttribute( name==PeopleItem.是否残疾, getValue(ValueType.INT)==EnumKeyValue.ENUM_是否_是 ) ) or FuseAttribute( name==PeopleItem.是否残疾, getValue(ValueType.INT)==EnumKeyValue.ENUM_是否_否 )  or (FuseLeafAttribute( name==PeopleItem.已享受的补贴项目_补贴项目, getValue(ValueType.INT) == EnumKeyValue.ENUM_补贴项目_残障补贴) )
	then 
		removedLeafAttrMap.put($fa.getLeafCode(), $fa.getName());
end

